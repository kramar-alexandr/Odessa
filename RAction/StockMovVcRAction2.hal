external function string 255 GetLegalInvoiceNrSerie(row LegalInvNrBlock,string);
external procedure GetLegalInvNrRow(string,var row LegalInvNrBlock);
external procedure B1ToB2StrValRM(string,val,val,var string,roundmode);
external procedure B1ToB2ValRM(val,val,val,var val,roundmode);
external function roundmode GetCostRoundModeRB();
external function Integer CheckObjs(string,string,var string);
external function string 60 AddObjectToObjectList(string,string);
external procedure AddPortugueseSAFTHashToStockMov(var record StockMovVc);
external updating procedure DeleteOffSerNr(LongInt,string);
external updating procedure UpdateOffSerNr(LongInt,string,Integer,string,Boolean);
external procedure FindNextStockMovVcOfficialSerialNr(var record StockMovVc,Integer);

external procedure WarnFutureDate(Boolean,Date);
external updating procedure UpdateStockResFromStockMov(record StockMovVc);
external function Boolean ItemHistExists(string,LongInt);
external function roundmode DefaultRoundMode();
external updating procedure CreateAutoProduction(record StockMovVc);
external updating procedure StockMovCreateStockMovement(record StockMovVc);
external updating function val NextLocOKNr(string);
external updating function Boolean UpdateForkLiftQueue(record StockMovVc,Boolean);
external updating procedure StockMovUpdateReserved(record StockMovVc,Boolean,Boolean);
external updating function Boolean UpdateIntOrderFromStockMov(record StockMovVc,record StockMovVc,Boolean);
external updating procedure StockMovSetPositionStatus(record StockMovVc,Integer);
external function Boolean CanOKStockRecord(var Integer);
external procedure ConvertToDualBase(var string,date,var val,var val,var val,var val,var val,var val,Boolean);
external procedure SwapM4Val(var val,var val);
external procedure B1ToB2StrVal(string,val,val,var string);
external procedure B1ToB2Val(val,val,val,var val);
external procedure StockMovSumUp(var record StockMovVc);
external updating procedure UpdateInStockMovFromStockMov(record StockMovVc,Boolean);
external updating procedure StockMovUpdateStock(record StockMovVc,record StockMovVc,Boolean,Boolean,Boolean);
external updating procedure StockMovUpdateItemHist(record StockMovVc,record StockMovVc);
external updating procedure StockMovUpdateCostPrice(record StockMovVc,record StockMovVc);
external updating procedure StockMovUpdateSerialNr(record StockMovVc,record StockMovVc,Boolean);
external procedure GetConsigmentStockAcc(string,string,var string);
external function Boolean ConsigmentStock(string,string,Date);
external procedure FindStockValueAtPosition(string,string,string,var record PISVc);
external function val FindCostPrice(string,string,string,val,val,Boolean);
external function Boolean TransInFiscal(Date);
external function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external function val MulRateToBase1(var string,val,val,val,val,val,val,roundmode);
external procedure PUCalcPerc(val,string,var val);
external procedure GetITStockAcc(string,var string);
external procedure AddEuroTrRowType(record TRVc,string,string,string,string,val,val,val,Boolean,Boolean,var val,Boolean,string,string,string,Integer);
external function Boolean GetAccName(string,var string,Integer);
external function Integer TypeOfCurncy(var string,var Integer);
external procedure GetFullCurncyRateText(Boolean,var string,val,val,val,val,val);
external procedure GetFullCurncyRate (var string,Date,var val,var val,var val,var val,var val);
external updating procedure UnOk_StockMovVc(var record StockMovVc, boolean);
external  function boolean CheckRemoveLogistics(record StockMovVc,var longint);
external updating procedure WriteProcessQtyBackToORVc(row StockMovVc,var row StockMovVc,boolean);
external updating procedure CreateLinksToOrders(record StockMovVc);
external updating procedure ReserveReceivedItems(record StockMovVc);
external updating procedure CheckDifferenceForWritingToORVc(var record StockMovVc,record StockMovVc);
external updating procedure UnOk_StockMovVc(var record StockMovVc, boolean);
external procedure FoundUniqueOrdersFromStockMov(record StockMovVc,var array longint);//Edit----------------------Dima  28.12.2015

global
function LongInt StockMovVcRecordDefaults(var record StockMovVc StockMovr,record StockMovVc StockMov2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  string 10 curcode;
  val fr,to1,to2,br1,br2;
  record UserVc Userr;	//Edit by Victor 26.11.14
  
  StockMovr.SerNr = -1;
  StockMovr.OrdTransDate = CurrentDate;
  if (SingleUserMode) then begin
    StockMovr.SerNr = NextSerNr("StockMovVc",StockMovr.OrdTransDate,-1,false,"");
  end;
  curcode = "";
  GetFullCurncyRate(curcode,StockMovr.OrdTransDate,fr,to1,to2,br1,br2);
  StockMovr.CurncyCode = curcode;
  StockMovr.FrRate = fr;
  StockMovr.ToRateB1 = to1; 
  StockMovr.ToRateB2 = to2;
  StockMovr.BaseRate1 = br1;
  StockMovr.BaseRate2 = br2;
  StockMovr.TotQty = blankval;
  StockMovr.TotWeight = blankval;
  StockMovr.TotVolume = blankval;
  StockMovr.ProdSerNr = -1;
  StockMovr.IntORNo = -1;
  StockMovr.FileName = "";
  StockMovr.TransNr = -1;
  if (HasLocalization("PRT")) then begin
    StockMovr.PlanSendTime = "";
    StockMovr.PlanSendDate = "";
  end;
  StockMovr.TerminalID = CurTerminalID;;
  StockMovr.BranchID = CurBranchID;
  StockMovr.LocalMachineCode = CurMachineName;
  StockMovr.OfficialSerNr = "";
  StockMovr.OfficialSerNrSerie = "";
  
  Userr.Code = CurrentUser;		//Edit by Victor 26.11.14
  if (ReadFirstMain(Userr,1,true)) then begin
	  StockMovr.SalesGroup = Userr.SalesGroup;
		StockMovr.FrLocation = Userr.Location;		//Edit-----------------Dima 02.02.2015
	end;

  StockMovVcRecordDefaults = res; 
  RETURN;
END;

global
function LongInt StockMovVcRecordDuplicate(var record StockMovVc StockMovr,record StockMovVc StockMov2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  string 10 curcode;
  val fr,to1,to2,br1,br2;
  integer i,rwcnt;
  row StockMovVc SMrw;
  Date bd;
  Time bt;

  StockMovr.SerNr = -1;
  StockMovr.OKFlag = 0;
  if (SingleUserMode) then begin
    StockMovr.SerNr = NextSerNr("StockMovVc",StockMovr.TransDate,-1,false,"");
  end;
  curcode = StockMovr.CurncyCode;
  GetFullCurncyRate(curcode,StockMovr.TransDate,fr,to1,to2,br1,br2);
  StockMovr.IntORNo = -1;
  StockMovr.ProdSerNr = -1;
  StockMovr.OrderNr = -1;
  StockMovr.FileName = "";
  StockMovr.SHNr = -1;
  StockMovr.TransNr = -1;
  StockMovr.TreatedFlag = 0;
  StockMovr.CurncyCode = curcode;
  StockMovr.FrRate = fr;
  StockMovr.ToRateB1 = to1; 
  StockMovr.ToRateB2 = to2;
  StockMovr.BaseRate1 = br1;
  StockMovr.BaseRate2 = br2;
  StockMovr.FrLocOKNr = blankval;
  StockMovr.ToLocOKNr = blankval;
  StockMovr.FrThrouLocOKNr = blankval;
  StockMovr.ToThrouLocOKNr = blankval;
  StockMovr.SentOKFlag = 0;
  StockMovr.Reserved = 0;
  StockMovr.OrdFlag = 0;
  StockMovr.RegDate = bd;
  StockMovr.RegTime = bt;
  WarnFutureDate(true,StockMovr.SentTransDate);
  WarnFutureDate(true,StockMovr.TransDate);
  StockMovr.OfficialSerNr = "";
  StockMovr.TerminalID = CurTerminalID;;
  StockMovr.BranchID = CurBranchID;
  StockMovr.LocalMachineCode = CurMachineName;
  StockMovr.Hash = "";
  StockMovr.HashKeyVersion = 0;
  StockMovr.TaxAdminServSeal = "";    
  StockMovr.OfficialSerNrSerie = "";
  if (HasLocalization("PRT")) then begin
    StockMovr.PlanSendTime = "";
    StockMovr.PlanSendDate = "";
  end;
  rwcnt = MatRowCnt(StockMovr);
  for(i=0;i<rwcnt;i=i+1) begin
		MatRowGet(StockMovr,i,SMrw);
		if (nonblank(SMrw.ORSerial)) then begin
			SMrw.ORSerial = "";
			SMrw.ORRowUID = blankval;
			SMrw.OrderProcessing = blankval;
			MatRowPut(StockMovr,i,SMrw);
		end;
	end;
	
  StockMovVcRecordDuplicate = res; 
  RETURN;
END;

global
function LongInt StockMovVcRecordReset(var record StockMovVc StockMovr,record StockMovVc StockMov2r,LongInt long3,LongInt long4)
begin
  LongInt res;
  
  StockMovr.OKFlag = 0;
  StockMovVcRecordReset = res;
  RETURN;
END;

procedure GetStockMovStockAcc(Integer ItemGroupAccounts,record LocationVc Locp,string itcode,string accusageacc,var string stockfracc)
BEGIN
  stockfracc = "";  
  stockfracc = Locp.StockAcc;
  if (nonblank(stockfracc)) then begin
    goto LGetStockMovFromStockAcc;
  end;
  if (ItemGroupAccounts!=0) then begin
    GetITStockAcc(itcode,stockfracc);
    if (nonblank(stockfracc)) then begin
      goto LGetStockMovFromStockAcc;
    end;
  end;
  stockfracc = accusageacc;
  if (nonblank(stockfracc)) then begin
    goto LGetStockMovFromStockAcc;
  end;
LGetStockMovFromStockAcc:;  
  RETURN;
END;

global
function Integer AddStockMovCostRows(record TRVc gTRp,record LocationVc LocFrp,record LocationVc LocTop,record CostAccBlock CostAccRec,record AccBlock ARAccRec,
                                    record StockMovVc StockMovp,row StockMovVc StockMovrw,
                                    Boolean dc1,Boolean dc2,Boolean sumupf,val br1,val br2,var val TotSum,Integer IntYc)
BEGIN
  Integer res;
  Boolean lsumupf;
  string 255 frobjstr,toobjstr,locobjstr;
  val rs2,rs,t;
  record INVc INr;
  string 10 stockfracc,stocktoacc;
  string 255 stockfrtxt,stocktotxt,errstr;
  Date blankd;
  
  lsumupf = sumupf;
  frobjstr = StockMovp.Objects;
  if (CostAccRec.ObjOnStock!=0) then begin
    if (nonblank(frobjstr)) and (nonblank(LocFrp.Objects)) then begin
      frobjstr = frobjstr & ",";
    end;
    frobjstr = frobjstr & LocFrp.Objects;
  end;
  if (nonblank(frobjstr)) and (nonblank(StockMovrw.Objects)) then begin
    frobjstr = frobjstr & ",";
  end;
  frobjstr = frobjstr & StockMovrw.Objects;

  toobjstr = StockMovp.Objects;
  if (CostAccRec.ObjOnStock!=0) then begin
    if (nonblank(toobjstr)) and (nonblank(LocTop.Objects)) then begin
      toobjstr = toobjstr & ",";
    end;
    toobjstr = toobjstr & LocTop.Objects;
  end;
  if (nonblank(toobjstr)) and (nonblank(StockMovrw.Objects)) then begin
    toobjstr = toobjstr & ",";
  end;
  toobjstr = toobjstr & StockMovrw.Objects;

  if (IntYc==SentSTMovYc) then begin
    rs2 = StockMovrw.SentOldPrice*StockMovrw.SentQuant;
    if (blank(rs2)) then begin
      rs2 = FindCostPrice(StockMovrw.ArtCode,StockMovp.FrLocation,StockMovrw.SerialNr,StockMovrw.SentQuant,0,false);
    end;
  end else begin
    rs2 = StockMovrw.OldPrice*StockMovrw.Quant;
    if (blank(rs2)) then begin
      rs2 = FindCostPrice(StockMovrw.ArtCode,StockMovp.FrLocation,StockMovrw.SerialNr,StockMovrw.Quant,0,false);
    end;
  end;
  if (ReadFirstItem(StockMovrw.ArtCode,INr,true,true)) then begin
  end;
  GetStockMovStockAcc(CostAccRec.ItemGroupAccounts,LocFrp,INr.Group,ARAccRec.StockAcc,stockfracc);
  locobjstr = LocFrp.Objects;
  GetConsigmentStockAcc(INr.Code,StockMovrw.SerialNr,stockfracc);
  if (ConsigmentStock(INr.Code,StockMovrw.SerialNr,blankd)) then begin
    lsumupf = true;
  end;
  if (GetAccName(stockfracc,stockfrtxt,60)==false) then begin
    res = 2120;
    goto LAddStockMovCostRows;
  end;
  rs2 = Round(rs2,DefaultRoundMode);
  locobjstr = AddObjectToObjectList(frobjstr,locobjstr);
  res = CheckObjs(stockfracc,locobjstr,errstr);
  if (res!=0) then begin
    goto LAddStockMovCostRows;
  end;

  AddEuroTrRowType(gTRp,stockfracc,locobjstr,"",stockfrtxt,rs2,br1,br2,lsumupf,dc2,TotSum,false,"","","",kTransactionRowTypeStock);
  if (IntYc==SentSTMovYc) then begin
    if (nonblank(StockMovrw.SentExtraSCost)) then begin
      PUCalcPerc(rs2,StockMovrw.SentExtraSCost,t);
      t = t*StockMovrw.Quant;
      if (GetAccName(ARAccRec.StockGainAcc,stockfrtxt,60)==false) then begin
        res = 2119;
        goto LAddStockMovCostRows;
      end;
      AddEuroTrRowType(gTRp,ARAccRec.StockGainAcc,toobjstr,"",stockfrtxt,t,br1,br2,lsumupf,dc2,TotSum,false,"","","",kTransactionRowTypeStock);
    end;
    t = StockMovrw.SentQuant*StockMovrw.SentNewPrice;
  end else begin
    if (nonblank(StockMovrw.ExtraSCost)) then begin
      PUCalcPerc(rs2,StockMovrw.ExtraSCost,t);
      t = t*StockMovrw.Quant;
      if (GetAccName(ARAccRec.StockGainAcc,stockfrtxt,60)==false) then begin
        res = 2119;
        goto LAddStockMovCostRows;
      end;
      AddEuroTrRowType(gTRp,ARAccRec.StockGainAcc,toobjstr,"",stockfrtxt,t,br1,br2,lsumupf,dc2,TotSum,false,"","","",kTransactionRowTypeStock);
    end;
    t = StockMovrw.Quant*StockMovrw.NewPrice;
  end;
  rs2 = MulRateToBase1(StockMovp.CurncyCode,t,StockMovp.FrRate,StockMovp.ToRateB1,StockMovp.ToRateB2,StockMovp.BaseRate1,StockMovp.BaseRate2,DefaultCurRoundOff);
  GetStockMovStockAcc(CostAccRec.ItemGroupAccounts,LocTop,INr.Group,ARAccRec.StockAcc,stocktoacc);
  locobjstr = LocTop.Objects;
  GetConsigmentStockAcc(INr.Code,StockMovrw.SerialNr,stocktoacc);
  locobjstr = AddObjectToObjectList(toobjstr,locobjstr);
  res = CheckObjs(stocktoacc,locobjstr,errstr);
  if (res!=0) then begin
    goto LAddStockMovCostRows;
  end;
  if (GetAccName(stocktoacc,stocktotxt,60)==false) then begin
    res = 2120;
    goto LAddStockMovCostRows;
  end;
  rs2 = Round(rs2,DefaultRoundMode);
  AddEuroTrRowType(gTRp,stocktoacc,locobjstr,"",stocktotxt,rs2,br1,br2,lsumupf,dc1,TotSum,false,"","","",kTransactionRowTypeStock);
LAddStockMovCostRows:;
  AddStockMovCostRows = res;
  RETURN;
END;

global
function Integer MakeTransFromStockMov(record TRVc gTRp,record StockMovVc StockMovp,record LocationVc LocFrp,record LocationVc LocTop,Boolean rpt,Integer IntYc)
BEGIN
  Integer res;
  record CostAccBlock CostAccRec;
  record TRGenBlock TRGenRec;
  record AccBlock ARAccRec;
  record TRVc locTRr;
  row StockMovVc StockMovrw;
  Integer i,rwcnt;
  string 255 ftxt;
  string 255 tstr,objstr;
  val t,frrate,to1,to2,br1,br2;
  val rs2,TotSum;
  Boolean dc1,dc2,sumupf,testf;
  Integer oldstyle;
  transaction string 255 gRuniningMaint;

  BlockLoad(ARAccRec);
  BlockLoad(TRGenRec);
  BlockLoad(CostAccRec);
  sumupf = true;
  if (CostAccRec.CorespondingAccounts!=0) then begin
    sumupf = false;
  end;
  dc1 = true;
  dc2 = false;
  rwcnt = MatRowCnt(StockMovp);
  frrate = StockMovp.FrRate;
  to1 = StockMovp.ToRateB1;
  to2 = StockMovp.ToRateB2;
  br1 = StockMovp.BaseRate1;
  br2 = StockMovp.BaseRate2;
  RecordNew(gTRp);
  if (rpt==false) then begin
    if (TRGenRec.StockMovGenTrans==0) then begin
      res = 0;
      goto LMakeTransFromStockMov;
    end;
    if (IntYc==SentSTMovYc) then begin
      if ((nonblankdate(TRGenRec.StockMovStartDate)) and
        (StockMovp.SentTransDate<TRGenRec.StockMovStartDate)) then begin 
        testf = true;
      end;
    end else begin
      if ((nonblankdate(TRGenRec.StockMovStartDate)) and
        (StockMovp.TransDate<TRGenRec.StockMovStartDate)) then begin 
        testf = true;
      end;
    end;
    if (testf) then begin
      res = 0;
      goto LMakeTransFromStockMov;
    end;
  end;
  gTRp.IntYc = IntYc;
  gTRp.Number = StockMovp.SerNr;
  if (rpt==false) then begin
    locTRr.Number = gTRp.Number;
    locTRr.IntYc = gTRp.IntYc;
    if (ReadFirstMain(locTRr,0,true)) then begin
      switch (gRuniningMaint) begin    
        case "RecalcStockMn":  ;
        otherwise
          res = 0;
          goto LMakeTransFromStockMov;
      end;
    end;
  end;
  gTRp.RegDate = CurrentDate;
  gTRp.RegTime = CurrentTime;
  if (IntYc==SentSTMovYc) then begin
    gTRp.TransDate = StockMovp.SentTransDate;
  end else begin
    gTRp.TransDate = StockMovp.TransDate;
  end;
  if (TransInFiscal(gTRp.TransDate)==false) then begin
    res = 1075;
    goto LMakeTransFromStockMov;
  end;
  if (ProgramType!=typHansaRaama) then begin
    if (nonblank(StockMovp.CurncyCode)) then begin
      ftxt = ftxt & StockMovp.CurncyCode;
      ftxt = ftxt & " ";
      GetFullCurncyRateText(true,tstr,StockMovp.FrRate,StockMovp.ToRateB1,StockMovp.ToRateB2,StockMovp.BaseRate1,StockMovp.BaseRate2);
      ftxt = ftxt & tstr;
    end else begin
      ftxt = StockMovp.Comment;
    end;
  end else begin
    if (TypeOfCurncy(StockMovp.CurncyCode,oldstyle)==0) then begin
      ftxt = ftxt & StockMovp.CurncyCode;
      ftxt = ftxt & " ";
      GetFullCurncyRateText(true,tstr,StockMovp.FrRate,StockMovp.ToRateB1,StockMovp.ToRateB2,StockMovp.BaseRate1,StockMovp.BaseRate2);
      ftxt = ftxt & tstr;
      ftxt = ftxt & " ";
    end;
    ftxt = ftxt & StockMovp.Comment;
  end;
  gTRp.Comment = ftxt;
  for (i=0; i<rwcnt; i=i+1) begin
    MatRowGet(StockMovp,i,StockMovrw);
    res = AddStockMovCostRows(gTRp,LocFrp,LocTop,CostAccRec,ARAccRec,StockMovp,StockMovrw,dc1,dc2,sumupf,br1,br2,TotSum,IntYc);
    if (res!=0) then begin
      goto LMakeTransFromStockMov;
    end;                                 
  end;
  if (TotSum!=0) then begin
    if (GetAccName(ARAccRec.StockGainAcc,tstr,60)==false) then begin
      res = 2119;
      goto LMakeTransFromStockMov;
    end;
    t = TotSum;
    AddEuroTrRowType(gTRp,ARAccRec.StockGainAcc,"","",tstr,t,br1,br2,true,false,TotSum,false,"","","",kTransactionRowTypeStock);
  end;
  res = 0;
LMakeTransFromStockMov:;
  MakeTransFromStockMov = res;
  RETURN;
END;

global
function Integer CheckStockMovRowFromPosition(record StockMovVc SMp,record StockMovVc SM2p,row StockMovVc StockMovrw,record LocationVc LocFrRec,record INVc INr,
                                              Integer rownr,Boolean sentquantf,var LongInt error,var string errorstr,var integer errorrownr,
                                              var string fieldstr,boolean UnOKedCheckSerialf)
BEGIN
  Integer res;
  string 20 frpos;
  record PISVc PISr;
  val quant;

  frpos = StockMovrw.FrPosCode;
  if (blank(frpos)) then begin
    frpos = SMp.FrPos;
  end;
  if (sentquantf) then begin
    quant = StockMovrw.SentQuant;
  end else begin
    quant = StockMovrw.Quant;
  end;
  if (LocFrRec.RequirePos!=0 and currentuser!="SAUNOK") then begin// Edit ************************** Thursday, 21 January 2016 09:59:09
    if (blank(frpos)) then begin
	 if (SM2p.OrdFlag==0) or (SM2p.SentOKFlag==0) then begin
	  error = 1854;
      errorstr = frpos;
      errorrownr = -1;
      fieldstr = "FrPos";                 
      res = 1854; 
      goto LCheckStockMovRowFromPosition;
	 end;
    end;      
    if (INr.ItemType==1) then begin
      if (frpos==LocFrRec.WHMDefPUPosCode) or (frpos==LocFrRec.WHMDefProdPosCode) then begin
      end else begin
        if (nonblank(SMp.ThrouLocation)) then begin
//missing funcionality        
        end else begin
          FindStockValueAtPosition(StockMovrw.ArtCode,LocFrRec.Code,frpos,PISr);
          if (quant>PISr.Instock) and (quant!=0) then begin
            error = 1397;
            errorstr = " " & PISr.Position;
            errorrownr = rownr;
            fieldstr = "Quant";                
            res = 1397; 
            goto LCheckStockMovRowFromPosition;
          end;
        end;
      end;
    end;  
  end;    
LCheckStockMovRowFromPosition:;
  CheckStockMovRowFromPosition = res;
  RETURN;
END;

global
updating function LongInt StockMovVcRecordImport(var record StockMovVc StockMovr,record StockMovVc StockMov2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  val t,fr,to1,to2,br1,br2;
  string 5 curncy;
  string 255 tstr;
  row StockMovVc StockMovrw;
  Integer i,rwcnt;
  Boolean gBase1ToBase2,gToDualBase;
  record ConvMasterBlock cvm;
  row LegalInvNrBlock LINrbrw;
      
  BlockLoad(cvm);
  if (cvm.DualBaseCurrencyFlag!=0) then begin gToDualBase = true; end;
  if (cvm.Base1ToBase2Flag!=0) then begin gBase1ToBase2 = true; end;
  if (gToDualBase) then begin
    curncy = StockMovr.CurncyCode;
    fr = StockMovr.FrRate;
    to1 = StockMovr.ToRateB1;
    to2 = StockMovr.ToRateB2;
    br1 = StockMovr.BaseRate1;
    br2 = StockMovr.BaseRate2;
    ConvertToDualBase(curncy,StockMovr.TransDate,fr,to1,to2,br1,br2,t,false);
    StockMovr.CurncyCode = curncy;
    StockMovr.FrRate = fr;
    StockMovr.ToRateB1 = to1;
    StockMovr.ToRateB2 = to2;
    StockMovr.BaseRate1 = br1;
    StockMovr.BaseRate2 = br2;
  end;
  if (gBase1ToBase2) then begin
    curncy = StockMovr.CurncyCode;
    fr = StockMovr.FrRate;
    to1 = StockMovr.ToRateB1;
    to2 = StockMovr.ToRateB2;
    br1 = StockMovr.BaseRate1;
    br2 = StockMovr.BaseRate2;
    SwapM4Val(br1,br2);
    SwapM4Val(to1,to2);
    rwcnt = MatRowCnt(StockMovr);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(StockMovr,i,StockMovrw);
      B1ToB2ValRM(StockMovrw.OldPrice,br1,br2,t,GetCostRoundModeRB);
      StockMovrw.OldPrice = t;
      B1ToB2StrValRM(StockMovrw.ExtraSCost,br1,br2,tstr,GetCostRoundModeRB);
      StockMovrw.ExtraSCost = tstr;
      B1ToB2ValRM(StockMovrw.NewPrice,br1,br2,t,GetCostRoundModeRB);
      StockMovrw.NewPrice = t;
      B1ToB2ValRM(StockMovrw.FIFORowVal,br1,br2,t,GetCostRoundModeRB);
      StockMovrw.FIFORowVal = t;
      B1ToB2Val(StockMovrw.BasePrice,br1,br2,t);
      StockMovrw.BasePrice = t;
      B1ToB2ValRM(StockMovrw.SentNewPrice,br1,br2,t,GetCostRoundModeRB);
      StockMovrw.SentNewPrice = t;
      B1ToB2StrValRM(StockMovrw.SentExtraSCost,br1,br2,tstr,GetCostRoundModeRB);
      StockMovrw.SentExtraSCost = tstr;
      B1ToB2ValRM(StockMovrw.SentOldPrice,br1,br2,t,GetCostRoundModeRB);
      StockMovrw.SentOldPrice = t;
      B1ToB2ValRM(StockMovrw.SentFIFORowVal,br1,br2,t,GetCostRoundModeRB);
      StockMovrw.SentFIFORowVal = t;
      MatRowPut(StockMovr,i,StockMovrw);
    end;    
    StockMovr.CurncyCode = curncy;
    StockMovr.FrRate = fr;
    StockMovr.ToRateB1 = to1;
    StockMovr.ToRateB2 = to2;
    StockMovr.BaseRate1 = br1;
    StockMovr.BaseRate2 = br2;
    StockMovSumUp(StockMovr);
  end;    
  if (StockMovr.TotQty==0) then begin
    StockMovSumUp(StockMovr);
  end;  
  if (blank(StockMovr.OfficialSerNrSerie)) then begin
    GetLegalInvNrRow(StockMovr.OfficialSerNr,LINrbrw);
    StockMovr.OfficialSerNrSerie = GetLegalInvoiceNrSerie(LINrbrw,StockMovr.OfficialSerNr);
  end;
  StockMovVcRecordImport = res;
  RETURN;
END;

global
updating function LongInt StockMovVcRecordImportAfter(var record StockMovVc StockMovr,record StockMovVc StockMov2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Integer err;
      
  StockMovUpdateSerialNr(StockMovr,StockMov2r,false);
  UpdateInStockMovFromStockMov(StockMovr,false);
  if (StockMovr.OKFlag==0) then begin
    StockMovUpdateStock(StockMovr,StockMov2r,false,false,false);  
  end else begin
    if (ImportingTextBackup==false and CanOKStockRecord(err)==true) then begin
      StockMovUpdateStock(StockMovr,StockMov2r,true,false,false);
      StockMovUpdateCostPrice(StockMovr,StockMov2r);
      StockMovUpdateItemHist(StockMovr,StockMov2r);
    end else begin
      StockMovUpdateStock(StockMovr,StockMov2r,false,false,false);  
    end;
  end;
  
  StockMovVcRecordImportAfter = res;
  RETURN;
END;





global
function LongInt StockMovVcRecordRemoveTest(var record StockMovVc StockMovr,record StockMovVc StockMov2r,LongInt long3,LongInt long4)
BEGIN
  LongInt res,ORsernr;
  record DBLockBlock DBLockRec;
  integer rownr;

  res = 1;
  BlockLoad(DBLockRec);
  if (StockMovr.OKFlag!=0) then begin
    if (StockMovr.TransDate<=DBLockRec.DeleteBeforeDate) then begin
      goto LStockMovVcRecordRemoveTest;
    end;
    if (long3>0) then begin
      MessageBox(1544,"");
    end;
    res = 0;
  end;
  if (StockMovr.SentOKFlag!=0) then begin
    if (StockMovr.SentTransDate<=DBLockRec.DeleteBeforeDate) then begin
      goto LStockMovVcRecordRemoveTest;
    end;
    if (long3>0) then begin
      MessageBox(1544,"");
    end;
    res = 0;
  end;
  if (StockMovr.OrdFlag!=0) then begin
    if (long3>0) then begin
      MessageBox(1544,"");
    end;
    res = 0;
  end;
  
  if(CheckRemoveLogistics(StockMovr,ORsernr)==false) then begin		//Edit----------------------Dima  06.10.2015         
        MessageBox(31123,ORsernr);              
        res = 0; 
        goto LStockMovVcRecordRemoveTest;  
	end;
  
  
LStockMovVcRecordRemoveTest:;
  StockMovVcRecordRemoveTest = res; 
  RETURN;
END;

global
updating function LongInt StockMovVcRecordRemove(var record StockMovVc StockMovr,record StockMovVc StockMov2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  integer rwcnt,i,rwcnt2,j,k;
  row ORVc ORrw;
  row StockMovVc StockMovrw;
  record ORVc ORr;
  array longint orders;
  boolean needstore;

  if (StockMovr.IntORNo!=-1) then begin
    if (UpdateIntOrderFromStockMov(StockMov2r,StockMovr,false)) then begin end;
  end;
  if (StockMovr.OKFlag==0) then begin
    StockMovSetPositionStatus(StockMovr,0);
  end;
  UpdateInStockMovFromStockMov(StockMovr,true);
  if (UpdateForkLiftQueue(StockMovr,true)) then begin end;
  
  if (nonblank(StockMovr.OfficialSerNr)) then begin 
    DeleteOffSerNr(StockMovr.SerNr,"StockMovVc");
  end;  
  
  FoundUniqueOrdersFromStockMov(StockMovr,orders);			//Edit----------------------Dima  28.12.2015
  rwcnt = MatRowCnt(StockMovr);  
  
  for(k=0;k<orders.length;k=k+1) begin
  	ORr.SerNr = orders[k];
  	if (ReadFirstMain(ORr,1,true)) then begin
  		rwcnt2 = MatRowCnt(ORr);
  		for(i=0;i<rwcnt;i=i+1) begin
  			MatRowGet(StockMovr,i,StockMovrw);
  			
				if (StockMovrw.ORSerial==orders[k]) then begin						//Edit----------------------Dima  18.08.2015
						needstore = true;
						for(j=0;j<rwcnt2;j=j+1) begin
							MatRowGet(ORr,j,ORrw);
							if ((ORrw.UID == StockMovrw.ORRowUID) and (ORr.SerNr == StockMovrw.ORSerial)) then begin
								ORrw.ProcessingQuant = ORrw.ProcessingQuant - StockMovrw.OrderProcessing;
								LogText(0,"remove...  SM " & StockMovr.SerNr & "  "& StockMovrw.ArtCode  &" processing qty " & "  SM: " & StockMovrw.OrderProcessing & " OR: " & ORrw.ProcessingQuant);
								MatRowPut(ORr,j,ORrw);
								//j = rwcnt2;
							end;
						end;
					end;				
			end;
			if (needstore) then begin
				RecordStore(ORr,true);
			end;			
						
		end;	//Edit----------------------Dima  20.08.2015
	end;
		    
  StockMovVcRecordRemove = res;
  RETURN;
END;

global
updating function LongInt StockMovVcRecordRemoveAfter(var record StockMovVc StockMovr,record StockMovVc StockMov2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;

  if (StockMovr.Reserved!=0) then begin
    StockMovUpdateReserved(StockMovr,true,true);
  end;
  StockMovVcRecordRemoveAfter = res;
  RETURN;
END;





global
updating function LongInt StockMovVcRecordUpdate(var record StockMovVc StockMovr,record StockMovVc StockMov2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Boolean ihef;
  row StockMovVc StockMovrw; //Edit***************************Sasha2,14:25 11.02.2015
  record INVc INr; //Edit***************************Sasha2,14:25 11.02.2015
  integer rwcnt,i; //Edit***************************Sasha2,14:25 11.02.2015
  record ItemStatusVc ISr; //Edit***************************Sasha2,11:56 25.08.2015
  record PISVc PISr;
  string 30 position;

  ihef = ItemHistExists("StockMovVc",StockMovr.SerNr);
  if ((StockMovr.SentOKFlag==0)) then begin
    if ((StockMovr.OKFlag!=0) and (StockMov2r.OKFlag==0)) then begin
      if (ihef==false) then begin
        StockMovr.FrLocOKNr = NextLocOKNr(StockMovr.FrLocation);       
      end;
    end;
  end else begin
    if ((StockMovr.SentOKFlag!=0) and (StockMov2r.SentOKFlag==0)) then begin
      if (ihef==false) then begin     
        StockMovr.FrLocOKNr = NextLocOKNr(StockMovr.FrLocation);
        StockMovr.ToThrouLocOKNr = NextLocOKNr(StockMovr.ThrouLocation);
      end;
    end;
  end;
  if ((StockMovr.OKFlag!=0) and (StockMov2r.OKFlag==0)) then begin
    if ((StockMovr.SentOKFlag!=0)) then begin
      if (ihef==false) then begin    
        StockMovr.FrThrouLocOKNr = NextLocOKNr(StockMovr.ThrouLocation);
      end;
    end;
    if (ihef==false) then begin
      StockMovr.ToLocOKNr = NextLocOKNr(StockMovr.ToLocation);
    end;
    if (ihef==false) then begin
      StockMovCreateStockMovement(StockMovr);
      StockMovSetPositionStatus(StockMovr,1);
    end;
  end;
  if ((StockMovr.IntORNo!=-1) and (StockMov2r.OKFlag==0)) then begin
    if (UpdateIntOrderFromStockMov(StockMovr,StockMov2r,false)) then begin end;
  end;  
  if (StockMovr.SentOKFlag!=0 and StockMov2r.SentOKFlag==0) then begin
    AddPortugueseSAFTHashToStockMov(StockMovr);
  end else begin
    if (StockMovr.OKFlag!=0) and (StockMov2r.OKFlag==0) and (StockMov2r.SentOKFlag==0) then begin
      AddPortugueseSAFTHashToStockMov(StockMovr);
    end;
  end;
  rwcnt = MatRowCnt(StockMovr); //Edit***************************Sasha2,14:27 11.02.2015 {
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(StockMovr,i,StockMovrw);
    INr.Code = StockMovrw.ArtCode;
    if (NonBlank(StockMovrw.ArtCode) and ReadFirstMain(INr,1,true)) then begin
      StockMovrw.BarCode = INr.BarCode;
      MatRowPut(StockMovr,i,StockMovrw);
    end;
    
    if(StockMovrw.stp!=1 and StockMovrw.stp!=0) then begin		//Edit----------------------Dima  23.02.2015
			StockMovrw.stp=1;
			MatRowPut(StockMovr,i,StockMovrw);
		end;
		
		MatRowGet(StockMovr,i,StockMovrw);    //Edit***************************Sasha2,11:54 25.08.2015 {
		position = "";
		position = StockMovrw.FrPosCode;
		if (blank(position)) then begin position = StockMovr.FrPos; end;

		if(nonblank(StockMovrw.ArtCode)) then begin
			if(nonblank(StockMovr.FrLocation))then begin	
				if (blank(position)) then begin							
					ISr.Code = StockMovrw.ArtCode;
					ISr.Location = StockMovr.FrLocation;
					if(ReadFirstMain(ISr, 2, true))then begin
						StockMovrw.FromQuant = ISr.Instock;
					end else begin
						StockMovrw.FromQuant = 0;
					end;
				end else begin
					PISr.ArtCode = StockMovrw.ArtCode;		//Edit----------------------Dima  19.01.2016
					PISr.Location = StockMovr.FrLocation;
					PISr.Position = position;
					if (ReadFirstMain(PISr,3,true)) then begin
						StockMovrw.FromQuant = PISr.Instock;
					end else begin
						StockMovrw.FromQuant = 0;
					end;
				end;
			end;
		
			position = "";
			position = StockMovrw.ToPosCode;
			if (blank(position)) then begin position = StockMovr.ToPos; end;
				
			if(nonblank(StockMovr.ToLocation))then begin
				if (blank(position)) then begin
					ISr.Code = StockMovrw.ArtCode;
					ISr.Location = StockMovr.ToLocation;
					if(ReadFirstMain(ISr, 2, true))then begin
						StockMovrw.ToQuant = ISr.Instock;
					end else begin
						StockMovrw.ToQuant = 0;
					end;
				end else begin
					PISr.ArtCode = StockMovrw.ArtCode;
					PISr.Location = StockMovr.ToLocation;
					PISr.Position = position;				
					if(ReadFirstMain(PISr, 3, true))then begin
						StockMovrw.ToQuant = PISr.Instock;
					end else begin
						StockMovrw.ToQuant = 0;
					end;
				end;
			end;
		
		end;
		MatRowPut(StockMovr,i,StockMovrw); //Edit***************************Sasha2,11:54 25.08.2015 }
		
  end; //Edit***************************Sasha2,14:27 11.02.2015 }
  
  CheckDifferenceForWritingToORVc(StockMovr,StockMov2r);//Edit----------------------Dima  19.08.2015
  
  if ((StockMovr.OKFlag==0) and (StockMov2r.OKFlag!=0)) then begin		//UnOk//Edit----------------------Dima  02.10.2015
    	UnOk_StockMovVc(StockMovr,true);
  end;
  if ((StockMovr.SentOKFlag==0) and (StockMov2r.SentOKFlag!=0)) then begin 
    	UnOk_StockMovVc(StockMovr,false);
  end;    	



  StockMovVcRecordUpdate = res;
  RETURN;
END;




global
updating function LongInt StockMovVcRecordSaveAfter(var record StockMovVc StockMovr,record StockMovVc StockMov2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  integer rwcnt,i;
  row StockMovVc StockMovrw;
  record ItemStatusVc ISr;

  StockMovUpdateStock(StockMovr,StockMov2r,true,false,false);
  StockMovUpdateCostPrice(StockMovr,StockMov2r);//Cost Price before history
  StockMovUpdateItemHist(StockMovr,StockMov2r);
  StockMovUpdateSerialNr(StockMovr,StockMov2r,false);
  
  if (StockMovr.OKFlag!=0) then begin
    StockMovSetPositionStatus(StockMovr,1);
    CreateAutoProduction(StockMovr);
  end else begin
    StockMovSetPositionStatus(StockMovr,2);
  end;
  if (StockMovr.IntORNo!=-1) then begin
    if (UpdateIntOrderFromStockMov(StockMovr,StockMov2r,false)) then begin end;
  end;
  UpdateInStockMovFromStockMov(StockMovr,false);
  if (StockMovr.OKFlag!=0) then begin
    if (UpdateForkLiftQueue(StockMovr,true)) then begin end;
  end else begin
    if (UpdateForkLiftQueue(StockMovr,false)) then begin end;
  end;
  if (nonblank(StockMovr.OfficialSerNr)) then begin
    UpdateOffSerNr(StockMovr.SerNr,"StockMovVc",StockMovr.StockMovType,StockMovr.OfficialSerNr,false);
  end;  
  
  //Edit by Victor 29.01.15 ***
  /*rwcnt = MatRowCnt(StockMovr);
	
	
	recordStore(StockMovr, true);
	*/
	CreateLinksToOrders(StockMovr);		//Edit----------------------Dima  20.08.2015
	if (StockMovr.OKFlag!=0)  then begin		//Edit----------------------Dima  20.08.2015
    ReserveReceivedItems(StockMovr);
  end; 	
	
	//***
  
  StockMovVcRecordSaveAfter = res;
  RETURN;
END;

global
updating function LongInt StockMovVcRecordSave(var record StockMovVc StockMovr,record StockMovVc StockMov2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  row StockMovVc StockMovrw,StockMovrw2; //Edit***************************Sasha2,14:25 11.02.2015
  record INVc INr; //Edit***************************Sasha2,14:25 11.02.2015
  integer rwcnt,i; //Edit***************************Sasha2,14:25 11.02.2015
  record ItemStatusVc ISr;
  record StockMovVc SMempty;
  record PISVc PISr;
  string 30 position;
  
  if (blankdate(StockMovr.RegDate)) then begin
    StockMovr.RegDate = CurrentDate;
  end;
  if (blanktime(StockMovr.RegTime)) then begin
    StockMovr.RegTime = CurrentTime;
  end;
  if ((StockMovr.SentOKFlag==0)) then begin
    if (StockMovr.OKFlag!=0) then begin
      StockMovr.FrLocOKNr = NextLocOKNr(StockMovr.FrLocation);
    end;  
  end else begin
    StockMovr.FrLocOKNr = NextLocOKNr(StockMovr.FrLocation);
    StockMovr.ToThrouLocOKNr = NextLocOKNr(StockMovr.ThrouLocation);
  end;
  if (StockMovr.OKFlag!=0) then begin
    if (StockMovr.SentOKFlag!=0) then begin
      StockMovr.FrThrouLocOKNr = NextLocOKNr(StockMovr.ThrouLocation);
    end;
    StockMovr.ToLocOKNr = NextLocOKNr(StockMovr.ToLocation);
    StockMovCreateStockMovement(StockMovr);
  end;
  if (blank(StockMovr.OfficialSerNr)) then begin
    FindNextStockMovVcOfficialSerialNr(StockMovr,-1);
  end;
  if (StockMovr.OKFlag!=0) or (StockMovr.SentOKFlag!=0) then begin
    AddPortugueseSAFTHashToStockMov(StockMovr);
  end;
  
  RecordNew(SMempty);
  RecordCopy(SMempty,StockMovr);	//Edit----------------------Dima  28.12.2015

  rwcnt = MatRowCnt(StockMovr); //Edit***************************Sasha2,14:27 11.02.2015 {
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(StockMovr,i,StockMovrw);
    INr.Code = StockMovrw.ArtCode;
    if (NonBlank(StockMovrw.ArtCode) and ReadFirstMain(INr,1,true)) then begin
      StockMovrw.BarCode = INr.BarCode;
      MatRowPut(StockMovr,i,StockMovrw);
    end;
    
    if(StockMovrw.stp!=1 and StockMovrw.stp!=0) then begin		//Edit----------------------Dima  23.02.2015
			StockMovrw.stp=1;
			MatRowPut(StockMovr,i,StockMovrw);
		end;	
		
		MatRowGet(StockMovr,i,StockMovrw);    //Edit***************************Sasha2,11:54 25.08.2015 {
		position = "";
		position = StockMovrw.FrPosCode;
		if (blank(position)) then begin position = StockMovr.FrPos; end;

		if(nonblank(StockMovrw.ArtCode)) then begin
			if(nonblank(StockMovr.FrLocation))then begin	
				if (blank(position)) then begin							
					ISr.Code = StockMovrw.ArtCode;
					ISr.Location = StockMovr.FrLocation;
					if(ReadFirstMain(ISr, 2, true))then begin
						StockMovrw.FromQuant = ISr.Instock;
					end else begin
						StockMovrw.FromQuant = 0;
					end;
				end else begin
					PISr.ArtCode = StockMovrw.ArtCode;		//Edit----------------------Dima  19.01.2016
					PISr.Location = StockMovr.FrLocation;
					PISr.Position = position;
					if (ReadFirstMain(PISr,3,true)) then begin
						StockMovrw.FromQuant = PISr.Instock;
					end else begin
						StockMovrw.FromQuant = 0;
					end;
				end;
			end;
		
			position = "";
			position = StockMovrw.ToPosCode;
			if (blank(position)) then begin position = StockMovr.ToPos; end;
				
			if(nonblank(StockMovr.ToLocation))then begin
				if (blank(position)) then begin
					ISr.Code = StockMovrw.ArtCode;
					ISr.Location = StockMovr.ToLocation;
					if(ReadFirstMain(ISr, 2, true))then begin
						StockMovrw.ToQuant = ISr.Instock;
					end else begin
						StockMovrw.ToQuant = 0;
					end;
				end else begin
					PISr.ArtCode = StockMovrw.ArtCode;
					PISr.Location = StockMovr.ToLocation;
					PISr.Position = position;				
					if(ReadFirstMain(PISr, 3, true))then begin
						StockMovrw.ToQuant = PISr.Instock;
					end else begin
						StockMovrw.ToQuant = 0;
					end;
				end;
			end;
		
		end;
		MatRowPut(StockMovr,i,StockMovrw); //Edit***************************Sasha2,11:54 25.08.2015 }
		
		//StockMov2r doesn't exist during "Save" procedure
		//So we create empty StockMov record to write difference (SM2r-SMr) to ORVc (logistics)
		//It has been done to keep the same code in the StockMovVcRecordSave and StockMovVcRecordUpdate
		MatRowGet(SMempty,i,StockMovrw);			//Edit----------------------Dima  28.12.2015
		if (StockMovrw.ORSerial>0) then begin
			StockMovrw.OrdQuant = 0;
			MatRowPut(SMempty,i,StockMovrw);
		end;			
				
  end; //Edit***************************Sasha2,14:27 11.02.2015 }
  
  
  CheckDifferenceForWritingToORVc(StockMovr,SMempty);//Edit----------------------Dima  28.12.2015
 
  StockMovVcRecordSave = res;
  RETURN;
END;

