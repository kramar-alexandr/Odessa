external function boolean MySendRawWebRequest(string, string, boolean, string, string, string, string, boolean, area, var area, integer);
external function string 255 ConvertXml(string);
external procedure GetXMLArea(var area);
external procedure GetXMLArea_check(var area,string);


function string 15 NormalizePhone(string phone)
begin
	string 15 correctPhone;

	correctPhone = StripNonDigits(phone);

	if (Len(correctPhone)==10) then begin
		correctPhone = "38" & Right(correctPhone,10);
	end else begin
		if(Len(correctPhone)<10)then begin
			correctPhone = "";
		end;
	end;
	
	NormalizePhone = correctPhone;
	return;
end;



global procedure SendSMSEmail(string phone, string message, string smsTitle)
begin
	area subject,body;
	string 15 correctPhone;
	record SMSSettingsBlock SMSblock;
	
	BlockLoad(SMSblock);

	if (DirExists("SMS")==false) then begin
		CreateFolder("SMS");
	end;

	correctPhone = NormalizePhone(phone);

	addtexttoarea(SMSblock.ePochtaKey & " " & correctPhone,subject);
	writeareatofile(subject,"SMS/sms_subj.txt",0);
	
	addtexttoarea("[SENDER]" & smsTitle & "[/SENDER]",body);
	addtexttoarea("[SMS]" & message & "[/SMS]",body);
	writeareatofile(body,"SMS/sms_body.txt",0);	
	//runprogram("SMS/sendSRVSMS.sh","");
	//logtext(0,"SEND SMS");
end;





procedure  CheckEPochtaResponse(var string response)	//Edit----------------------Dima  30.06.2015
begin
	integer v;
	v = StringToInt(response);
		
	if (v <= 0) then begin
		switch(v) begin
			case -1: response = USetStr(31075);
			case -2: response = USetStr(31076);
			case -3: response = USetStr(31077);
			case -4: response = USetStr(31078);
			otherwise response = USetStr(31079);
		end;
	end else begin
			response = "";
	end;	

end;

procedure CreateSMSRequest(string aWeb,string phone,string text,string title,string ordclass,var area aRequest)	//Edit----------------------Dima  30.06.2015
begin
  area request,replyarea,test;
  string 100 tstr;
  string 20 timed;
  area basestring;
  record SMSSettingsBlock SMSb;
  string 50 login,password;
  integer i;   
  
  BlockLoad(SMSb);  

  login = SMSb.Login;
  password = SMSb.Password;
  
  if(ordclass=="WEB")then begin
		login = SMSb.WEBLogin;
		password = SMSb.WEBPassword;
  end;
		
	Addtexttoarea("POST " & aWeb  & chr(13) & chr(10),aRequest);
 	AddTextToArea("" & chr(13) & chr(10),aRequest);

  AddTextToArea("<SMS>"& chr(13),aRequest);
  AddTextToArea("<operations><operation>" & "SEND" & "</operation></operations>" & chr(13),aRequest);
  AddTextToArea("<authentification>"  & chr(13),aRequest);
  AddTextToArea("<username>" & login & "</username>" & chr(13),aRequest);  
  AddTextToArea("<password>" & password & "</password>" & chr(13),aRequest); 
  AddTextToArea("</authentification>"  & chr(13),aRequest);  
  AddTextToArea("<message>"& chr(13),aRequest);
  AddTextToArea("<sender>" & title & "</sender>" & chr(13),aRequest); 
  AddTextToArea("<text>" & text & "</text>" & chr(13),aRequest);
  AddTextToArea("</message>" & chr(13),aRequest);
  AddTextToArea("<numbers>"& chr(13),aRequest);
  AddTextToArea("<number>" & ConvertXml(phone) & "</number>" & chr(13),aRequest); 
  AddTextToArea("</numbers>" & chr(13),aRequest);  
  AddTextToArea("</SMS>" & chr(13),aRequest);  
	AddTextToArea("" & chr(13) & chr(10),aRequest);
	
	
	writeareatofile(aRequest,"SMSRequest.txt",0);
  
return;
end;





global updating
function string 255 SendSMS(record SMSstackVc SMSr, boolean writeLog)
begin
  area replyarea,mainRequest;
  string 200 host,page,response;
  xml xdata;
  string 15 correctPhone;

	response = "";
  
  //host = "my.epochta.ru";
  host = "api.atompark.com";
  page = "/members/sms/xml.php"; 
   
  correctPhone = NormalizePhone(SMSr.Phone);
  
	CreateSMSRequest(host & page,correctPhone,SMSr.Text,SMSr.Title,SMSr.OrderClass, mainRequest);   
   

  if(MySendRawWebRequest(host,"80",false,"POST",page ,"text/xml","",false,mainRequest,replyarea,5))then begin 
    writeareatofile(replyarea,"SMSReply.txt",0);
    GetXMLArea_check(replyarea,"</RESPONSE>");
    if(GetAreaLength(replyarea)>0)then begin
			xdata = ParseXMLArea(replyarea);

			response = XmlGet(xdata,"RESPONSE/status");
			CheckEPochtaResponse(response);
		
			//if (writeLog) then begin 
				LogText(0,"SMS was sent.  TEXT: " & SMSr.Text);
			//end;	
		end else begin
			logtext(0,"Hansa World: No Connection to Epochta");
			response = "Hansa World: No Connection to Epochta";
			SMSr.ReSend = 1;
		end;	 
		   
  end else begin
 		
  end;
  
  SMSr.Phone = correctPhone;
  SMSr.SendDate = CurrentDate;
  SMSr.SendTime = CurrentTime; 
  SMSr.Error = response;
  if (blank(response)) then begin
		SMSr.Sent = 1;
		SMSr.ReSend = 0;
	end;
  RecordStore(SMSr,true);  

 	SendSMS = response;
  return;
end;


global updating function CreateSMSlinks(record SMSstackVc SMSr,string docType,longint docSerNr)
begin
record ORVc ORr;
record SHvc SHr;
	switch(docType) begin
		case "ORVc":
				ORr.SerNr =  docSerNr;
		  	if (ReadFirstMain(ORr,1,true)) then begin	
  				CreateRecordLink(ORr,CurrentCompany,SMSr,CurrentCompany);  
  			end;
		case "SHVc":
				SHr.SerNr =  docSerNr;
		  	if (ReadFirstMain(SHr,1,true)) then begin	
  				CreateRecordLink(SHr,CurrentCompany,SMSr,CurrentCompany);  
  			end; 				  			
	end;
end;


global updating function longint AddSMStoStack(string phone, string title, string text,string custcode,string docType,longint docSerNr,string OrderClass,date sendDate,time sendTime) 
begin
record SMSstackVc SMSr;
longint smsNr;

  RecordNew(SMSr);
  
  SMSr.SerNr = NextSerNr("SMSstackVc",CurrentDate,-1,false,"");
  SMSr.Phone = phone;
  if(left(phone,1)=="+")then begin
  	SMSr.Phone = right(phone,len(phone)-1);
  end;
  SMSr.Title = title;
  SMSr.Text = text;
  SMSr.CustCode = custcode;
  if (BlankDate(sendDate)) then begin
  	SMSr.CreationDate = CurrentDate;
  end else begin
		SMSr.CreationDate = sendDate;
	end;
	if (BlankTime(sendTime)) then begin
	  SMSr.CreationTime = CurrentTime; 
	end else begin 
		SMSr.CreationTime = sendTime; 
	end; 
  SMSr.SourceType = docType;
  SMSr.SourceSerNr = docSerNr;
  SMSr.OrderClass = OrderClass;
  if (RecordInsert(SMSr,true)) then begin
		smsNr = SMSr.SerNr;
	end;
	CreateSMSlinks(SMSr,docType,docSerNr);
	
  AddSMStoStack = smsNr;
end;



global updating
procedure  SendSMSFromStack()		//Edit----------------------Dima  28.08.2015
begin
	record SMSstackVc SMSr;
	boolean TrHs;
	string 100 response;

	
	SMSr.Sent = 0;
	TrHs = true;
	While(LoopBackKey("Sent",SMSr,1,TrHs)) begin
		if (SMSr.Sent != 0) then begin TrHs = false; end;
		if (TrHs and ((SMSr.CreationDate<CurrentDate) or (SMSr.CreationDate==CurrentDate and SMSr.CreationTime<=CurrentTime)) and (blank(SMSr.Error) or (SMSr.ReSend==1)) ) then begin
			//queued.SendSMS2(SMSr.Phone,SMSr.Text,SMSr.Title,false,SMSr.OrderClass);
			response = SendSMS(SMSr,true);
		end;	
	end;
  
end;



global updating
function string 255  CheckAndSendSMS(longint sernr)		//Edit----------------------Dima  28.08.2015
begin
	record SMSstackVc SMSr;
	string 100 response;

	SMSr.SerNr = sernr;
	if (ReadFirstMain(SMSr,1,true)) then begin
		response = SendSMS(SMSr,true);
	end;	
	
  CheckAndSendSMS = response;
end;






/*
//Previous function without writing to SMSstackVc
global 
function string 255 SendSMS(string phone,string text,string title, boolean writeLog,string ordclass)
begin
  area replyarea,mainRequest;
  string 200 host,page,response;
  xml xdata;
  string 15 correctPhone;

	response = "";
   
  
  host = "my.epochta.ru";
  page = "/members/sms/xml.php"; 
   
  correctPhone = NormalizePhone(phone);
  
	CreateSMSRequest(host & page,correctPhone,text,title,ordclass, mainRequest);   
   

  if(MySendRawWebRequest(host,"80",false,"POST",page ,"text/xml","",false,mainRequest,replyarea,5))then begin 
   // writeareatofile(replyarea,"SMSReply.txt",0);
    GetXMLArea_check(replyarea);
		xdata = ParseXMLArea(replyarea);

		response = XmlGet(xdata,"RESPONSE/status");
		CheckEPochtaResponse(response);
		
		if (writeLog) then begin 
			LogText(0,"SMS was sent.  TEXT: " & text);
		end;		 
		   
  end else begin
 		logtext(0,"Hansa World: No Connection to my.epochta.ru");
 		response = "Hansa World: No Connection to my.epochta.ru";
  end;
 
	LSendSMS:; 
 	SendSMS = response;
  return;
end;

*/